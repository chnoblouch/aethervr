use std.{memory, config};

use xr;

use aethervr.{
    log,
    swapchain.SwapchainImage,
    graphics.{vulkan, d3d11, metal},
};

struct ImageRegion {
    var x: i32;
    var y: i32;
    var width: i32;
    var height: i32;
    var array_index: u32;
}

struct ImageData {
    var width: u32;
    var height: u32;
    var data: Array[u8];
}

proto GraphicsBackend {
    pub func new(info: *xr.SessionCreateInfo) -> ?*GraphicsBackend {
        # TODO: Free the memory allocated here.

        var next = info.next as *xr.BaseInStructure;

        meta if config.OS == config.WINDOWS || config.OS == config.LINUX {
            if next.type_ == xr.StructureType.GRAPHICS_BINDING_VULKAN_KHR {
                log.info("Graphics binding: Vulkan");

                var binding = next as *xr.GraphicsBindingVulkanKHR;
                return memory.box(VulkanBackend.new(*binding));
            }
        }
        
        meta if config.OS == config.WINDOWS {
            if next.type_ == xr.StructureType.GRAPHICS_BINDING_D3D11_KHR {
                log.info("Graphics binding: D3D11");

                var binding = next as *xr.GraphicsBindingD3D11KHR;
                return memory.box(D3D11Backend.new(*binding));
            }
        }

        meta if config.OS == config.MACOS {
            if next.type_ == xr.StructureType.GRAPHICS_BINDING_METAL_KHR {
                log.info("Graphics binding: Metal");

                var binding = next as *xr.GraphicsBindingMetalKHR;
                return memory.box(MetalBackend.new(*binding));
            }
        }

        log.error("Graphics binding is unsupported");
        return none;
    }

    func id(self) -> u32;
    func get_supported_formats(self) -> [i64];
    func create_image(self, info: *xr.SwapchainCreateInfo, image: *SwapchainImage) -> xr.Result;
    func store_image(self, handle: usize, out_images: *xr.SwapchainImageBaseHeader, out_index: u32);
    func share_image(self, image: *SwapchainImage) -> ?usize;
    func wait_image(self, image: *SwapchainImage, timeout: i64) -> xr.Result;
    func release_image(self, image: *SwapchainImage);
}

meta if config.OS == config.WINDOWS || config.OS == config.LINUX {
    struct VulkanBackend: GraphicsBackend {
        var binding: xr.GraphicsBindingVulkanKHR;

        pub func new(binding: xr.GraphicsBindingVulkanKHR) -> VulkanBackend {
            return VulkanBackend { binding };
        }

        pub func id(self) -> u32 {
            return 0;
        }

        pub func get_supported_formats(self) -> [i64] {
            return vulkan.get_supported_formats();
        }

        pub func create_image(mut self, info: *xr.SwapchainCreateInfo, image: *SwapchainImage) -> xr.Result {
            return vulkan.create_swapchain_image(info, &self.binding, image);
        }

        pub func store_image(self, handle: usize, out_images: *xr.SwapchainImageBaseHeader, out_index: u32) {
            var out_images_vulkan = out_images as *xr.SwapchainImageVulkanKHR;
            out_images_vulkan[out_index].image = handle as addr;
        }

        pub func share_image(mut self, image: *SwapchainImage) -> ?usize {
            return none;
            # return vulkan.share_image(&self.binding, image);
        }

        pub func wait_image(self, image: *SwapchainImage, timeout: i64) -> xr.Result {
            return xr.Result.SUCCESS;
        }

        pub func release_image(self, image: *SwapchainImage) {
            # HACK
            vulkan.vk.device_wait_idle(self.binding.device);
        }
    }
}

meta if config.OS == config.WINDOWS {
    struct D3D11Backend: GraphicsBackend {
        var binding: xr.GraphicsBindingD3D11KHR;

        pub func new(binding: xr.GraphicsBindingD3D11KHR) -> D3D11Backend {
            return D3D11Backend { binding };
        }

        pub func id(self) -> u32 {
            return 1;
        }

        pub func get_supported_formats(self) -> [i64] {
            return d3d11.get_supported_formats();
        }

        pub func create_image(mut self, info: *xr.SwapchainCreateInfo, image: *SwapchainImage) -> xr.Result {
            return d3d11.create_swapchain_image(info, &self.binding, image);
        }

        pub func store_image(self, handle: usize, out_images: *xr.SwapchainImageBaseHeader, out_index: u32) {
            var out_images_d3d11 = out_images as *xr.SwapchainImageD3D11KHR;
            out_images_d3d11[out_index].texture = handle as addr;
        }

        pub func share_image(mut self, image: *SwapchainImage) -> ?usize {
            return d3d11.share_image(&self.binding, image);
        }

        pub func wait_image(mut self, image: *SwapchainImage, timeout: i64) -> xr.Result {
            return d3d11.wait_image(&self.binding, image, timeout);
        }

        pub func release_image(mut self, image: *SwapchainImage) {
            d3d11.release_image(&self.binding, image);
        }
    }
}

meta if config.OS == config.MACOS {
    struct MetalBackend: GraphicsBackend {
        var binding: xr.GraphicsBindingMetalKHR;

        pub func new(binding: xr.GraphicsBindingMetalKHR) -> MetalBackend {
            return MetalBackend { binding };
        }

        pub func id(self) -> u32 {
            return 2;
        }

        pub func get_supported_formats(self) -> [i64] {
            return metal.get_supported_formats();
        }

        pub func create_image(mut self, info: *xr.SwapchainCreateInfo, image: *SwapchainImage) -> xr.Result {
            return metal.create_swapchain_image(info, &self.binding, image);
        }

        pub func store_image(self, handle: usize, out_images: *xr.SwapchainImageBaseHeader, out_index: u32) {
            var out_images_metal = out_images as *xr.SwapchainImageMetalKHR;
            out_images_metal[out_index].texture = handle as addr;
        }

        pub func share_image(self, image: *SwapchainImage) -> ?usize {
            return none;
        }

        pub func wait_image(self, image: *SwapchainImage, timeout: i64) -> xr.Result {
            return xr.Result.SUCCESS;
        }

        pub func release_image(self, image: *SwapchainImage) {}
    }
}
