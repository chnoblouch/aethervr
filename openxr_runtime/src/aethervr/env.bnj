use std.{config, path.Path};

meta if config.OS == config.WINDOWS {
    const GET_MODULE_HANDLE_EX_FLAG_UNCHANGED_REFCOUNT: u32 = 0x00000002;
    const GET_MODULE_HANDLE_EX_FLAG_FROM_ADDRESS: u32 = 0x00000004;

    @[link_name=GetCurrentProcessId]
    native func get_current_process_id() -> u32;

    @[link_name=GetModuleHandleExA]
    native func get_module_handle_ex(dwFlags: u32, lpModuleName: *u8, phModule: *addr) -> u32;
    
    @[link_name=GetModuleFileNameA]
    native func get_module_file_name(hModule: addr, lpFilename: *u8, nSize: u32) -> u32;

    pub func process_id() -> u32 {
        return get_current_process_id();
    }

    pub func get_shared_library_path() -> ?Path {
        var handle: addr;
        if get_module_handle_ex(
            GET_MODULE_HANDLE_EX_FLAG_UNCHANGED_REFCOUNT | GET_MODULE_HANDLE_EX_FLAG_FROM_ADDRESS,
            get_shared_library_path as *u8,
            &handle,
        ) == 0 {
            return none;
        }

        var path_str: [u8; 255];
        if get_module_file_name(handle, &path_str as *u8, 255) == 0 {
            return none;
        }

        return Path.from(&path_str as *u8);
    }
} else {
    struct DlInfo {
        var fname: *u8;
        var fbase: addr;
        var sname: *u8;
        var saddr: addr;
    }
    
    native func getpid() -> i32;
    native func dladdr(addr_: addr, info: *DlInfo) -> i32;

    pub func process_id() -> u32 {
        return getpid() as u32;
    }

    pub func get_shared_library_path() -> ?Path {
        var dl_info: DlInfo = undefined;

        if dladdr(get_shared_library_path, &dl_info) == 0 {
            return none;
        }
        
        return Path.from(String.from(dl_info.fname));
    }
}
