use std.memory;

use xr;

use aethervr.{
    graphics,
    constants,
    log,
    session.Session,
    utils.handle_buffer_size,
};

enum SwapchainImageState {
    AVAILABLE,
    ACQUIRED,
    WAITED,
    BEGUN,
}

struct SwapchainImage {
    var id: u32;
    var create_info: xr.SwapchainCreateInfo;
    var api_handle: usize;
    var api_memory: addr;
    var api_opaque_values: [u64; 2];
    var state: SwapchainImageState;
}

struct Swapchain {
    var session: *Session;
    var usage_flags: u64;
    var images: [SwapchainImage];
    var last_released_index: usize;

    pub func create(session: *Session, create_info: *xr.SwapchainCreateInfo) -> (*Swapchain, xr.Result) {
        log.info("Creating swapchain");

        # Raise an error if create flags were provided because there are currently none implemented.
        if create_info.create_flags != 0 {
            log.warn("Unsupported create flags, cannot create swapchain");
            return (null, xr.Result.ERROR_FEATURE_UNSUPPORTED);
        }

        log.enter_scope();

        log.info("Format: %", create_info.format);
        log.info("Sample count: %", create_info.sample_count);
        log.info("Width: %", create_info.width);
        log.info("Height: %", create_info.height);
        log.info("Face count: %", create_info.face_count);
        log.info("Array size: %", create_info.array_size);
        log.info("Mip count: %", create_info.mip_count);

        var supported_formats = session.graphics_backend.get_supported_formats();
        var format_supported = false;

        for supported_format in supported_formats {
            if create_info.format == supported_format {
                format_supported = true;
                break;
            }
        }

        if !format_supported {
            log.error("Unsupported swapchain format");
            return (null, xr.Result.ERROR_SWAPCHAIN_FORMAT_UNSUPPORTED);
        }

        var images: [SwapchainImage] = [];

        for i in 0..constants.NUM_SWAPCHAIN_IMAGES {
            var image: SwapchainImage;
            var result = session.graphics_backend.create_image(create_info, &image);

            if result != xr.Result.SUCCESS {
                # TODO: This is a memory leak
                log.exit_scope();
                return (null, result);
            }

            image.id = session.last_image_id;
            session.last_image_id += 1;

            images.append(image);
        }

        var swapchain = memory.box(Swapchain {
            session,
            usage_flags: create_info.usage_flags,
            images,
            last_released_index: 0,
        });

        for ref mut image in swapchain.images {
            try shared_handle in session.graphics_backend.share_image(&image) {
                session.tracker_connection.register_image(&image, shared_handle);
            }
        }

        log.exit_scope();
        return (swapchain, xr.Result.SUCCESS);
    }

    pub func enumerate_images(
        mut self,
        image_capacity_input: u32,
        image_count_output: *u32,
        images: *xr.SwapchainImageBaseHeader
    ) -> xr.Result {
        log.info("Enumerating swapchain images");

        var buffer_info = handle_buffer_size(self.images.length() as u32, image_count_output, image_capacity_input);
        if buffer_info.0 {
            return buffer_info.1;
        }

        for i in 0..self.images.length() {
            var handle = self.images[i].api_handle;
            self.session.graphics_backend.store_image(handle, images, i as u32);
        }

        return xr.Result.SUCCESS;
    }

    pub func acquire_image(mut self, info: *xr.SwapchainImageAcquireInfo, index: *u32) -> xr.Result {
        for i in 0..self.images.length() {
            if self.images[i].state == SwapchainImageState.AVAILABLE {
                *index = i as u32;
                self.images[i].state = SwapchainImageState.ACQUIRED;
                return xr.Result.SUCCESS;
            }
        }

        return xr.Result.ERROR_CALL_ORDER_INVALID;
    }

    pub func wait_image(mut self, info: *xr.SwapchainImageWaitInfo) -> xr.Result {
        for i in 0..self.images.length() {
            if self.images[i].state == SwapchainImageState.ACQUIRED {
                self.images[i].state = SwapchainImageState.WAITED;
                return self.session.graphics_backend.wait_image(&self.images[i], info.timeout);
            }
        }

        return xr.Result.ERROR_CALL_ORDER_INVALID;
    }

    pub func release_image(mut self, info: *xr.SwapchainImageReleaseInfo) -> xr.Result {        
        for i in 0..self.images.length() {
            if self.images[i].state == SwapchainImageState.WAITED {
                self.session.graphics_backend.release_image(&self.images[i]);
                self.images[i].state = SwapchainImageState.AVAILABLE;
                self.last_released_index = i;
                return xr.Result.SUCCESS;
            }
        }

        return xr.Result.ERROR_CALL_ORDER_INVALID;
    }
}
